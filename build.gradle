buildscript {

    ext {
        kotlinVersion = '1.7.0'
        springBootVersion = '2.7.1'
        springBootVersionTest = '2.8.4'
        springKafkaVersion = '2.8.4'
        archunitVersion = '0.23.1'
        epLoggingVersion = '1.0.24'
        epMetricsVersion = '0.5.0'
        logstashLogbackVersion = '7.1'
        jacksonKotlinVersion = '2.13.3'
        oidcTokenSupportVersion = '2.0.14'
        oidcTokenSupportTestVersion = '2.0.5'

        // TEST
        mockkVersion = '1.12.4'
        ninjaMockkVersion = '3.1.1'
        mockserverNettyVersion = '5.13.2'
    }

    repositories {
        mavenCentral()

        maven {
            url "https://maven.pkg.github.com/navikt/maven-release"
            credentials {
                username = "token"
                password = "$System.env.GITHUB_TOKEN"
            }
        }
    }

}

plugins {
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}"
    id "org.jetbrains.kotlin.plugin.spring" version "$kotlinVersion"
    id "org.jetbrains.kotlin.plugin.allopen" version "${kotlinVersion}"
    id 'org.springframework.boot' version "${springBootVersion}"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id "org.owasp.dependencycheck" version "7.1.1"
    id 'com.adarshr.test-logger' version '3.2.0'
    id "org.sonarqube" version "3.4.0.2513"
    id 'jacoco'
}

assert JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17): "Java 17 or newer is required"

group = 'no.nav.eessi.pensjon'

dependencies {

    // Spring-boot BOM-fil spesifiserer versjoner for Spring Boot-avhengigheter
    implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-actuator")

    // Spring Framework BOM-fil overstyrer så vi får siste patch-versjon av Spring Framework
    implementation(platform("org.springframework:spring-framework-bom:5.3.21"))

    implementation "org.springframework.kafka:spring-kafka:$springKafkaVersion"

    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jacksonKotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    implementation("io.micrometer:micrometer-registry-prometheus")
    implementation("no.nav.eessi.pensjon:ep-metrics:${epMetricsVersion}")

    // Logging
    implementation("net.logstash.logback:logstash-logback-encoder:${logstashLogbackVersion}")
    configurations { all*.exclude group: 'commons-logging', module: 'commons-logging' }
    implementation group: 'org.slf4j', name: 'jcl-over-slf4j'
    implementation("no.nav.eessi.pensjon:ep-logging:${epLoggingVersion}")

    //Auth
    implementation("no.nav.security:token-client-spring:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-spring:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-jaxrs:${oidcTokenSupportVersion}")
    implementation("no.nav.security:token-validation-test-support:${oidcTokenSupportTestVersion}")

    // TEST
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude module: 'junit'
        exclude module: "mockito-core"
        exclude module: "junit-vintage-engine"
    }
    testImplementation "org.springframework.kafka:spring-kafka-test:$springBootVersionTest"

    testImplementation(platform("org.junit:junit-bom:5.8.2"))
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.jupiter:junit-jupiter-params")

    // Next version (5.5.4) fails, should be investigated
    testImplementation("org.mock-server:mockserver-client-java:${mockserverNettyVersion}"){
    }
    testImplementation("org.mock-server:mockserver-netty:${mockserverNettyVersion}") {
    }
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation("com.ninja-squad:springmockk:$ninjaMockkVersion")
    implementation('net.logstash.logback:logstash-logback-encoder:7.0.1')
    implementation('ch.qos.logback:logback-classic:1.2.11')

    // Architecture tests
    testImplementation "com.tngtech.archunit:archunit:${archunitVersion}"
}

test {
    useJUnitPlatform()

    // set JVM arguments for the test JVM(s)
    failFast = true
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ["-Xjsr305=strict"]
        jvmTarget = "17"
    }
}

compileTestKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjsr305=strict']
        jvmTarget = '17'
    }
}

// https://docs.gradle.org/current/userguide/jacoco_plugin.html
jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

tasks.sonarqube.dependsOn tasks["jacocoTestReport"]

repositories {

    mavenCentral()

    ["maven-release", "ep-metrics"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("gpr.key")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or gpr.key, see README")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = "token"
                password = token
            }
        }
    }

}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}

/* https://github.com/ben-manes/gradle-versions-plugin */
dependencyUpdates {
    rejectVersionIf {
        ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'pr']
                .any { qualifier -> it.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/ }
    }
    revision = 'release'
}